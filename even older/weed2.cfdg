CF::Impure = 1
CF::Background = [ b -1]
startshape start

curlOnSplit = 5

shape start {
//head
	spinner(10)[y -6 s 27 18 b .1 h 160 a -.8 ]
	spinner(-10)[y -6 s 27 18 b .1 h 200 a -.8 ]
	spinner(20)[y -6 s 27 18 b .1 h 160 a -.85 ]
	spinner(-20)[y -6 s 27 18 b .1 h 200 a -.85 ]
//	CIRCLE[y -6 s 27 18 b .1 h 50 a -.85 ]
	spinner(20)[x 1 y -14 s 13 10 b .14 h 10  a -.7 ]
	spinner(-20)[x -1 y -14 s 13 10 b .14 h 10  a -.7 ]
//eyes
	CIRCLE[x 3.5 y -6.5 s 4 b .2 a -.5 ]
	CIRCLE[x 3.5 y -6.5 s 5 b .2 a -.5 ]
	spinner(-.1)[x 3.5 y -6.5 s 4]
	CIRCLE[x -3.5 y -6.5 s 4 b .2 a -.5 ]
	CIRCLE[x -3.5 y -6.5 s 5 b .2 a -.5 ]
	spinner(.1)[x -3.5 y -6.5 s 4]
//kittyface
	spinner(3)[x -4 y -12 s 10 10 r 90 flip 180 b .12 h 100 a -.2  ]
	spinner(3)[ y -12 s 10 6 r 90 b .12 h 0 a -.2  ]
	spinner(3)[x 4 y -12 s 10 10 r -90 b .12 h 100 a -.2  ]
	spinner(3)[ y -12 s 10 6 r 90 flip 180 b .12 h 0 a -.2  ]
//beard
	trunk2[s 1 1.5 r 180 y -18 h 180]
	trunk2[s 1 1.5 r 180 y -18 h 180 flip 90]
//ears
	catEar[x 4 r -40]
	catEar[x -4 r 40 flip 90]
}

shape catEar {
//	loop i=10 [x .05 y -.1 r 1] 
		trunk(0)[]
}

shape trunk2 {
	trunk(0)[x -3]
	trunk2[ r -15 s .8 x 4.5 y -.5]
}

shape trunk (number curl)
rule {
	spinner(2..20)[s 2 1 h 70]
	trunk(curl)[y .5 s .97 r (-2..2 + curl) h (0Â±10)]
//	trunk(curl)[x 2 s .4]
}
rule .2 {
	trunk(curl)[]
	trunk(curl)[x 5 s .4]
}

shape spinner (number twist) 
rule {
	CIRCLE[b 1 sat .5 a -.95]
	spinner(twist)[s .86 .97 r twist]
}
rule 5 {
	CIRCLE[b 1 sat .5 a -.99]
	spinner(twist)[s .92 .985 r twist]
}