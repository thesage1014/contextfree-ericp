CF::Background=[b -1]
CF::MinimumSize=4
startshape seed(100) // chance in percent to run shape

shape seed(number chance) { // chance in percent
	if(rand(100)<=chance)
		cloud[s 1.5 rand(.4, 1) r rand(-20, 20) b .5 sat -1] //b rand(.7, 1)
}

shape cloud
rule {
	poof[s 1 1 sat -1 a -rand(.9, 1)]
	cloud[ s rand(1) a -(rand()+.4) ]
	cloud[ s rand(.7, 1) x rand(1,-0.5) y rand(.5,-0.5 ) b rand(-.1,.2) ]
}
gS = 4 //gap size
rule .2 {
	cloud[x rand(gS,-gS) y rand(gS,-gS )]
}


//poof
shape poof
rule {
	loop 2 [s .9] CIRCLE[ b -.05 sat .1 h 27]
	go[r rand(360)]
	go[r rand(360)]
}

shape go
rule {
	oc[r rand(-90, 90)]
	go[ s rand(.8, .85) x -.05 y rand(-.2,.2)]
}
rule .1{
	go[]
	go[[x .5 r rand(-50,50) x -.5]]
}

shape oc
rule {
	TRIANGLE[ x .35 s .7 sat .1 h 50 r rand(360)]
}
rule {
	SQUARE[ x .35 s .7 sat .1 h 50  r rand(360)]
}
rule {
	CIRCLE[ x .35 s .7 sat .1 h 50  r rand(360)]
}
rule {
	oc[s .5 x rand(-1,1) y rand(-1,1)]
	oc[s .5 x rand(-1,1) y rand(-1,1)] 
}
